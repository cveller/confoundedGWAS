// This script simulates random mating for 150 generations, then same-trait assortative mating for traits X for 20 generations, followed by random mating for a further 20 generations. The same-trait assortative mating involves a constant female-male correlation of 0.2 for trait-X among mating pairs. Trait X is encoded by 1000 loci evenly spaced throughout the (autosomal) genome. In each generation, a population GWAS and a sibling GWAS are performed for trait X across all 1000 loci.

initialize() {
	initializeSLiMModelType("nonWF");
	
	defineConstant("map", "Human");	// "Human" or "stylized" (one chrom of length 1 Morgan)
	defineConstant("mapFolder", "/Users/Who/folderWhereLinkageMapsAre/");
	
	defineConstant("resultsFolder", "/Users/Who/folderToWhereResultsShouldBeWritten/");
	
	defineConstant("N", 40000);	// no. individuals in the population; must be a multiple of 8
	
	defineConstant("L", 1000);	// number of loci
	defineConstant("Loci", 0:(L-1)); // list of loci
	defineConstant("aX", rnorm(L,0,1)); // per-allele effect on trait X
	defineConstant("MAF", 0.1); // minimum minor-allele freq
	defineConstant("initialCounts", asInteger(runif(L, asInteger(MAF*2*N), asInteger((1-MAF)*2*N)))); 
	// initial counts of focal allele at each locus
	
	// set up useful matrices with which to calculate trait values
	defineConstant("XEffectMat1", matrix(rep(aX,1),nrow=1,byrow=T));
	defineConstant("XEffectMat", matrix(rep(aX,N),nrow=N,byrow=T));
	defineConstant("XEffectMat2", matrix(rep(aX,asInteger(2*N)),nrow=2*N,byrow=T));
	defineConstant("femaleInds", 0:(N/2-1)); // female indices
	defineConstant("maleInds", (N/2):(N-1)); // male indices (SLiM convention)
	defineConstant("IsMale", c(rep(0,N),rep(1,N)));
	defineConstant("IsMale1", c(rep(0,asInteger(N/2)),rep(1,asInteger(N/2))));
	
	// desired female-male correlation for trait X among mates, with proportionate error tolerance
	defineConstant("CorrAim", 0.2);
	defineConstant("CorrTol", 0.01);
	defineConstant("CorrAttempts", 5); // number of reorderings per noise size
												  // (for a given noise size, the realized correlation is noisy)
	
	// set up temporary files for use in sibling GWAS each generation
	defineConstant("GenoDiffTrackingFile", resultsFolder+"GenoDiffTracking.csv");
	defineConstant("XDiffTrackingFile", resultsFolder+"XDiffTracking.csv");
	
	// set up files to write results to
	defineConstant("PopGWAS", resultsFolder+"PopGWAS.csv");	
	defineConstant("SibGWAS", resultsFolder+"SibGWAS.csv");
	
	initializeMutationRate(0);
	initializeMutationType("m1", 0.5, "f", 0.0);	// defines the ref allele at each locus
	m1.convertToSubstitution = F;
	initializeGenomicElementType("g1", m1, 1.0);
	initializeGenomicElement(g1, 0, L-1);
	
	initializeSex("A");
	
	// input (interpolated) human linkage map, or stylized one-chrom map of length 1 Morgan
	if (map == "Human")
	{
	
	// input male recombination
	lines = readFile(mapFolder + "linkageMapForSLiM_Human_Male_1kLoci.txt");
	rates = NULL;
	for (line in lines)
	{
		components = strsplit(line, "\t");
		rates = c(rates, asFloat(components[0]));
	}
	initializeRecombinationRate(rates, 1:(L-1), sex="M");
	
	// input female recombination
	lines = readFile(mapFolder + "linkageMapForSLiM_Human_Female_1kLoci.txt");
	rates = NULL;
	for (line in lines)
	{
		components = strsplit(line, "\t");
		rates = c(rates, asFloat(components[0]));
	}
	initializeRecombinationRate(rates, 1:(L-1), sex="F");
	
	}
	else
	initializeRecombinationRate(1/(L-1));

}

1 early() {
	sim.addSubpop("p1", N);
}

// Introduce reference alleles at predetermined freq at each locus, in Hardy-Weinberg and linkage equilibrium (in expectation). 
1 early() {
	
	for (l in Loci)
	{
		sampledGenomes_locusl = sample(p1.individuals.genomes, initialCounts[l]);
		sampledGenomes_locusl.addNewMutation(m1, 0, l);
	}

}

// Perform population GWAS

2: late() {
	
	inds = sim.subpopulations.individuals;
	
	countAlleles = "asInteger(inds.genome1.containsMarkerMutation(m1, applyValue)) + asInteger(inds.genome2.containsMarkerMutation(m1, applyValue));";
// function to count # reference alleles at each locus
	
	Genos = sapply(Loci, countAlleles)-1;
	Genos = matrix(Genos, nrow = N); // NxL matrix, element (n,l) = # ref alleles (minus 1) carried by indiv n at locus l
	
	XScores = Genos*XEffectMat; // per-locus contributions to each indiv's value of trait X
	XScores = apply(XScores, 0, "sum(applyValue);"); // trait X values
	
	
	// perform population-wide GWAS on trait X at every locus l
	XBetas = NULL;	// effect size estimates
	p = NULL;	// reference allele frequencies
	for (l in Loci){
		
		LocusGenos = c(Genos[,l]);	// genotypes at locus l
		p = c(p,(sum(LocusGenos)+N)/(2*N)); // "+N" because of the genotype normalization to -1, 0, +1
		popXGWAS = cov(LocusGenos,XScores)/var(LocusGenos);	// OLS regression of trait-X value on genotype at l
		XBetas = c(XBetas, popXGWAS);	// collect slope
	
	}
	
	// write to results files
	writeFile(PopGWAS, ""+mean(XBetas^2), append=T);
	writeFile(PopGWAS, ""+mean(2*p*(1-p)*XBetas^2), append=T);

}

// Perform sib GWAS

2: late() {

// further down this script is code to generate, each generation, a list of sibling differences in genotype and in trait values, written to the files "GenoDiffTrackingFile" and "XDiffTrackingFile"
	
	// to calculate allele freqs:
	inds = sim.subpopulations.individuals;
	
	countAlleles = "asInteger(inds.genome1.containsMarkerMutation(m1, applyValue)) + asInteger(inds.genome2.containsMarkerMutation(m1, applyValue));";
	// function to count # reference alleles at each locus
	
	Genos = sapply(Loci, countAlleles)-1;
	Genos = matrix(Genos, nrow = N); // NxL matrix, element (n,l) = # ref alleles (minus 1) carried by indiv n at locus l
	
	// for the sib GWAS itself:
	GenoDiff = readFile(GenoDiffTrackingFile);
	GenoDiff = matrix(GenoDiff, nrow = asInteger(N/2),  byrow=T );
	// GenoDiff[n,l] is the difference in # reference alleles at locus l in the nth sibship
	
	XDiff = readFile(XDiffTrackingFile);
	XDiff = matrix(XDiff, nrow = asInteger(N/2));
	// XDiff[n,l] is the difference in trait-X values in the nth sibship
	XDiffs = c(asFloat(XDiff));
	
	// perform sibling GWAS for trait X at every locus l
	
	p = NULL;	// will be a list of freqs of ref alleles
	XBetas = NULL;	// will be a list of effect size estimates
	for (l in Loci)
	{	
		// calculate reference allele freq
		LocusGenos = c(Genos[,l]);
		p = c(p,(sum(LocusGenos)+N)/(2*N)); // "+N" because of the genotype normalization to -1, 0, +1
		
		// perform sib GWAS
		LocusGenoDiffs = c(asFloat(GenoDiff[,l]));
		sibXGWAS = cov(LocusGenoDiffs,XDiffs)/var(LocusGenoDiffs);	// sib GWAS on trait X
		XBetas = c(XBetas, sibXGWAS);	// collect slope
	}
	
	// write to results files	
	writeFile(SibGWAS, ""+mean(XBetas^2), append=T);
	writeFile(SibGWAS, ""+mean(2*p*(1-p)*XBetas^2), append=T);
	
	// delete temporary data storage files
	deleteFile(GenoDiffTrackingFile);
	deleteFile(XDiffTrackingFile);

}

// kill off parents each generation, to maintain constant population size
early() {
	// life table based individual mortality
	inds = p1.individuals;
	ages = inds.age;
	inds.fitnessScaling = 1-ages; // non-overlapping generations
}

// burn in
// Random mating, so we randomly permute males and females and have them mate according to these permuted orderings. 

1:150 reproduction() {
	
	inds = sim.subpopulations.individuals;
	
	countAlleles = "asInteger(inds.genome1.containsMarkerMutation(m1, applyValue)) + 			asInteger(inds.genome2.containsMarkerMutation(m1, applyValue));";
	countAllelesOffspring1 = "asInteger(offspring1.genome1.containsMarkerMutation(m1, applyValue)) + 			asInteger(offspring1.genome2.containsMarkerMutation(m1, applyValue));";
	countAllelesOffspring2 = "asInteger(offspring2.genome1.containsMarkerMutation(m1, applyValue)) + 			asInteger(offspring2.genome2.containsMarkerMutation(m1, applyValue));";
	
	// random mating	
	
	// permute males and females randomly
	permuteFemale = sample(0:(asInteger(N/2)-1), asInteger(N/2));
	permuteMale = sample(0:(asInteger(N/2)-1), asInteger(N/2));	
	
	// have female-male mating pairs form according to their randomly permuted ordering
	FemaleInds = inds[0:(N/2-1)];
	MaleInds = inds[(N/2):(N-1)];
	FemaleIndsOrdered = FemaleInds[permuteFemale];
	MaleIndsOrdered = MaleInds[permuteMale];
	
	// first quarter of mating pairs produce two daughters each; next half produce one son and one daughter each; remaining quarter produce two sons each -- this keeps sex ratio even in next gen
	mateCount = 0;
	for (i in 0:(N/2-1))
	{
		mateCount = mateCount + 1;
		
		FemaleMate = FemaleIndsOrdered[i];
		MaleMate = MaleIndsOrdered[i];
		
		if (mateCount < N/8 + 0.5)
		{
			sex1 = "F";
			sex2 = "F";
		}
		else
		{if (mateCount > N/8 + 0.5  &  mateCount < 3*N/8 + 0.5)
			{
				sex1 = "F";
				sex2 = "M";
			}
			else
			{sex1 = "M";
				sex2 = "M";
			}
		}
		
		offspring1 = subpop.addCrossed(FemaleMate, MaleMate, sex1);
		offspring2 = subpop.addCrossed(FemaleMate, MaleMate, sex2);
		
		// calculate differences between each offspring 1, offspring 2 pair in their genotypes and in their trait values		
		
		GenoOffspring1 = sapply( Loci, countAllelesOffspring1) - 1;
		GenoOffspring1 = matrix(GenoOffspring1, nrow = 1);
		
		GenoOffspring2 = sapply( Loci, countAllelesOffspring2) - 1;
		GenoOffspring2 = matrix(GenoOffspring2, nrow = 1);
		
		GenoDiff = GenoOffspring1 - GenoOffspring2;
		
		XScoreOffspring1 = GenoOffspring1*XEffectMat1;
		XScoreOffspring1 = apply(XScoreOffspring1, 0, "sum(applyValue);");
		
		XScoreOffspring2 = GenoOffspring2*XEffectMat1;
		XScoreOffspring2 = apply(XScoreOffspring2, 0, "sum(applyValue);");
		
		XDiff = XScoreOffspring1 - XScoreOffspring2;
		
		// write sibling diffs in genotypes and in trait values to temporary storage files, which we'll use to perform the sibling GWAS
		writeFile(GenoDiffTrackingFile, ""+GenoDiff, append=T);
		writeFile(XDiffTrackingFile, ""+XDiff, append=T);
	
	}
	
	// disable this callback for this generation
	self.active = 0;

}




// assortative mating for 20 gens
// Determine female-male mating pairs to achieve desired female-male correlation for trait X values

151:170 reproduction() {
	
	inds = sim.subpopulations.individuals;
	//catn(inds.age);
	
	countAlleles = "asInteger(inds.genome1.containsMarkerMutation(m1, applyValue)) + 			asInteger(inds.genome2.containsMarkerMutation(m1, applyValue));";
	// function to count # reference alleles at each locus across all individuals
	countAllelesOffspring1 = "asInteger(offspring1.genome1.containsMarkerMutation(m1, applyValue)) + 			asInteger(offspring1.genome2.containsMarkerMutation(m1, applyValue));";
	countAllelesOffspring2 = "asInteger(offspring2.genome1.containsMarkerMutation(m1, applyValue)) + 			asInteger(offspring2.genome2.containsMarkerMutation(m1, applyValue));";
	// function to count # reference alleles at each locus in offspring
	
	Genos = sapply(Loci, countAlleles) - 1;
	Genos = matrix(Genos, nrow = N);
	
	XScores = Genos*XEffectMat;
	XScores = apply(XScores, 0, "sum(applyValue);");	// values for trait X
	
	XScoresFemale = XScores[femaleInds];
	XScoresMale = XScores[maleInds];
	
	maxCorr = cor( sort(XScoresFemale), sort(XScoresMale) ); // maximum correlation possible given female and male trait scores, which obtains when they are assorted based on exact rank
	
	if (maxCorr < CorrAim) // If the maximum correlation is smaller than the desired correlation...
	{
		FemalesOrdered = sort(XScoresFemale);
		FemaleOrdering = order(XScoresFemale);
		MalesOrdered = sort(XScoresMale);
		MaleOrdering = order(XScoresMale); // ... males and females mate based on exact rank
		catn("this happened");
	}
	else	// perform iterative procedure to get an assortment of mates with desired correlation
	{
		
		CorrNoiseNow = 4*( max( c(XScoresFemale,XScoresMale) - 1 ) - min( c(XScoresFemale,XScoresMale) - 1 ) );
		CorrNoisePrev = 2*CorrNoiseNow;
		CorrTooSmallPrev = 1;
		CorrErr = 1;
		
		while (CorrErr > CorrTol)	// until the correlation is within our error tolerance of the desired correlation
		{
			
//			catn(CorrNoiseNow);
//			catn(CorrNoisePrev);
			
			// perturb male and female trait values by random independent amounts;
			// do this CorrAttempts times
			FemaleNoise = rnorm(asInteger(CorrAttempts*N/2), 0, CorrNoiseNow);
			MaleNoise = rnorm(asInteger(CorrAttempts*N/2), 0, CorrNoiseNow);
			
			XScoresFemalePseudo = c(rep(XScoresFemale,CorrAttempts)) + FemaleNoise;
			XScoresMalePseudo = c(rep(XScoresMale,CorrAttempts)) + MaleNoise;
			
			CorrErr = NULL;	
			FemalesOrdered = NULL;		
			FemaleOrdering = NULL;
			MalesOrdered = NULL;		
			MaleOrdering = NULL;
			
			// for each of the CorrAttempts perturbations of trait values, rank order females and males based on their perturbed trait values and calculate the correlation if they mate according to this ranking; is it within the tolerance envelope of the desired correlation, too high, or too low?
			for (Attempt in 0:(CorrAttempts-1))	{		
			
			indices = asInteger(Attempt*N/2):asInteger((Attempt+1)*N/2 - 1);	
			
			XScoresFemalePseudoAttempt = XScoresFemalePseudo[indices];
			XScoresMalePseudoAttempt = XScoresMalePseudo[indices];
			
			FemalesOrderedAttempt = sort(XScoresFemalePseudoAttempt);
			FemalesOrdered = c(FemalesOrdered,FemalesOrderedAttempt);
			FemaleOrderingAttempt = order(XScoresFemalePseudoAttempt);
			FemaleOrdering = c(FemaleOrdering,FemaleOrderingAttempt);
			
			MalesOrderedAttempt = sort(XScoresMalePseudoAttempt);
			MalesOrdered = c(MalesOrdered,MalesOrderedAttempt);
			MaleOrderingAttempt = order(XScoresMalePseudoAttempt);
			MaleOrdering = c(MaleOrdering,MaleOrderingAttempt);
			
			CorrNowAttempt = cor(XScoresFemale[FemaleOrderingAttempt], XScoresMale[MaleOrderingAttempt]);
			CorrErrAttempt = abs( CorrNowAttempt - CorrAim )/CorrAim;
			
			CorrErr = c(CorrErr,CorrErrAttempt);
			
			}
			
			BestAttempt = whichMin(CorrErr);	// choose the perturbation that gave the female-male correlation closest to our desired correlation
			indices = asInteger(BestAttempt*N/2):asInteger((BestAttempt+1)*N/2 - 1);
			FemalesOrdered = FemalesOrdered[indices];
			FemaleOrdering = FemaleOrdering[indices];
			MalesOrdered = MalesOrdered[indices];
			MaleOrdering = MaleOrdering[indices];
			
			CorrNow = cor(XScoresFemale[FemaleOrdering], XScoresMale[MaleOrdering]);
			catn(CorrNow);
			//CorrNow = cor(FemalesOrdered, MalesOrdered);
			
			CorrErr = abs( CorrNow - CorrAim )/CorrAim;
			catn(( CorrNow - CorrAim )/CorrAim);
			//catn(CorrErr);
			
			CorrTooSmallNow = (CorrNow < CorrAim);
			catn(CorrTooSmallNow);
			catn(CorrTooSmallPrev);
			
			CorrNoiseNowPlaceholder = CorrNoiseNow;
			
			
			if (CorrTooSmallNow)	// If corr too small this time...
			{
				if (CorrTooSmallPrev)	// ... and too small last time,
				{
					CorrNoiseNow = CorrNoiseNow/2; // make perturbation size smaller.
				}
				else						// ... but too big last time,
				{
					CorrNoiseNow = (CorrNoiseNow + CorrNoisePrev)/2; // choose intermediate perturbation size.
				}
			}
			else					// If corr too big this time...
			{
				if (CorrTooSmallPrev)	// ... but too small last time,
				{
					CorrNoiseNow = (CorrNoiseNow + CorrNoisePrev)/2; // choose intermediate perturbation size.
				}
				else						// ... and too big last time,
				{
					CorrNoiseNow = CorrNoiseNow*2; // make perturbation size bigger
				}
			}
			
			CorrNoisePrev = CorrNoiseNowPlaceholder;
			CorrTooSmallPrev = CorrTooSmallNow;
		
		}
				
	}
	
	FemaleInds = inds[0:(N/2-1)];
	MaleInds = inds[(N/2):(N-1)];
	FemaleIndsOrdered = FemaleInds[FemaleOrdering];
	MaleIndsOrdered = MaleInds[MaleOrdering];
	permute = sample(0:(asInteger(N/2)-1), asInteger(N/2));
	FemaleIndsOrdered = FemaleIndsOrdered[permute];
	MaleIndsOrdered = MaleIndsOrdered[permute];
	
	// Now that we've decided on an assortment of mates that results in the desired cross-trait correlation, we'll have these mating pairs produce two offspring each. We'll randomly permute the ordering of the mating pairs. To keep an even sex ratio in the next generation, the first 25% of mating pairs will produce two daughters, the next 50% will produce a son and a daughter, and the remaining 25% of mating pairs will produce two sons.
	
	mateCount = 0;
	for (i in 0:(N/2-1))
	{
		mateCount = mateCount + 1;
		
		FemaleMate = FemaleIndsOrdered[i];
		MaleMate = MaleIndsOrdered[i];
		
		if (mateCount < N/8 + 0.5)
		{
			sex1 = "F";
			sex2 = "F";
		}
		else
		{if (mateCount > N/8 + 0.5  &  mateCount < 3*N/8 + 0.5)
			{
				sex1 = "F";
				sex2 = "M";
			}
			else
			{sex1 = "M";
				sex2 = "M";
			}
		}
		
		offspring1 = subpop.addCrossed(FemaleMate, MaleMate, sex1);
		offspring2 = subpop.addCrossed(FemaleMate, MaleMate, sex2);
		
		// calculate differences between each offspring 1, offspring 2 pair in their genotypes and in their trait values
		
		GenoOffspring1 = sapply( Loci, countAllelesOffspring1) - 1;
		GenoOffspring1 = matrix(GenoOffspring1, nrow = 1);
		
		GenoOffspring2 = sapply( Loci, countAllelesOffspring2) - 1;
		GenoOffspring2 = matrix(GenoOffspring2, nrow = 1);
		
		GenoDiff = GenoOffspring1 - GenoOffspring2;
		
		XScoreOffspring1 = GenoOffspring1*XEffectMat1;
		XScoreOffspring1 = apply(XScoreOffspring1, 0, "sum(applyValue);");
		
		XScoreOffspring2 = GenoOffspring2*XEffectMat1;
		XScoreOffspring2 = apply(XScoreOffspring2, 0, "sum(applyValue);");
		
		XDiff = XScoreOffspring1 - XScoreOffspring2;
		
		// write sibling diffs in genotypes and in trait values to temporary storage files, which we'll use to perform the sibling GWAS
		writeFile(GenoDiffTrackingFile, ""+GenoDiff, append=T);
		writeFile(XDiffTrackingFile, ""+XDiff, append=T);
	
	}
	
	// disable this callback for this generation
	self.active = 0;

}




// Random mating in remaining generations; same as in gens 1-150, so see above for commented code
171: reproduction() {
	
	inds = sim.subpopulations.individuals;
	//catn(inds.age);
	
	countAlleles = "asInteger(inds.genome1.containsMarkerMutation(m1, applyValue)) + 			asInteger(inds.genome2.containsMarkerMutation(m1, applyValue));";
	countAllelesOffspring1 = "asInteger(offspring1.genome1.containsMarkerMutation(m1, applyValue)) + 			asInteger(offspring1.genome2.containsMarkerMutation(m1, applyValue));";
	countAllelesOffspring2 = "asInteger(offspring2.genome1.containsMarkerMutation(m1, applyValue)) + 			asInteger(offspring2.genome2.containsMarkerMutation(m1, applyValue));";
	
	permuteFemale = sample(0:(asInteger(N/2)-1), asInteger(N/2));
	permuteMale = sample(0:(asInteger(N/2)-1), asInteger(N/2));	
	
	FemaleInds = inds[0:(N/2-1)];
	MaleInds = inds[(N/2):(N-1)];
	FemaleIndsOrdered = FemaleInds[permuteFemale];
	MaleIndsOrdered = MaleInds[permuteMale];
	
	mateCount = 0;
	for (i in 0:(N/2-1))
	{
		mateCount = mateCount + 1;
		
		FemaleMate = FemaleIndsOrdered[i];
		MaleMate = MaleIndsOrdered[i];
		
		if (mateCount < N/8 + 0.5)
		{
			sex1 = "F";
			sex2 = "F";
		}
		else
		{if (mateCount > N/8 + 0.5  &  mateCount < 3*N/8 + 0.5)
			{
				sex1 = "F";
				sex2 = "M";
			}
			else
			{sex1 = "M";
				sex2 = "M";
			}
		}
		
		offspring1 = subpop.addCrossed(FemaleMate, MaleMate, sex1);
		offspring2 = subpop.addCrossed(FemaleMate, MaleMate, sex2);
		
		GenoOffspring1 = sapply( Loci, countAllelesOffspring1) - 1;
		GenoOffspring1 = matrix(GenoOffspring1, nrow = 1);
		
		GenoOffspring2 = sapply( Loci, countAllelesOffspring2) - 1;
		GenoOffspring2 = matrix(GenoOffspring2, nrow = 1);
		
		GenoDiff = GenoOffspring1 - GenoOffspring2;
		
		XScoreOffspring1 = GenoOffspring1*XEffectMat1;
		XScoreOffspring1 = apply(XScoreOffspring1, 0, "sum(applyValue);");
		
		XScoreOffspring2 = GenoOffspring2*XEffectMat1;
		XScoreOffspring2 = apply(XScoreOffspring2, 0, "sum(applyValue);");
		
		XDiff = XScoreOffspring1 - XScoreOffspring2;
		
		writeFile(GenoDiffTrackingFile, ""+GenoDiff, append=T);
		writeFile(XDiffTrackingFile, ""+XDiff, append=T);
	
	
	}
	
	// disable this callback for this generation
	self.active = 0;

}

191 late() {sim.simulationFinished();}
